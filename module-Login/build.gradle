plugins {
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
}
if (enableLoginModule.toBoolean()) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

def root_android = rootProject.ext.android
def root_dependencies = rootProject.ext.dependencies

android {
    compileSdk build_versions.compile_sdk

    defaultConfig {
        minSdk build_versions.min_sdk
        targetSdk build_versions.target_sdk
        versionCode root_android.versionCode
        versionName root_android.versionName

        if (!enableLoginModule.toBoolean()) {
            //单独调试时添加applicationId集成调试时移除
            applicationId "com.kingz.demo.login"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

         // Arouter的Kotlin配置
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }

        viewBinding{
            enabled=true
        }
    }

    sourceSets {
        main {
            // 单独调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (enableLoginModule.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField("boolean", "SPLSH_DEBUG", "true")
            zipAlignEnabled true
            minifyEnabled false
        }
        release {
            buildConfigField("boolean", "SPLSH_DEBUG", "true")
            zipAlignEnabled true
            minifyEnabled false
        }
    }

    compileOptions {
        // 解决 Invoke-customs are only supported starting with Android O 报错.
        // Reason: kotlin 字节码的问题
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
    namespace 'com.zeke.module_login'
}

dependencies {
//    if(!isLoginModule){
//        implementation project(':module-Common')
//    }
    implementation root_dependencies["arouter"]
    kapt root_dependencies["arouter_compiler"]

    implementation project(path: ':module-Common')

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation deps.appcompat
    implementation 'androidx.core:core-ktx:1.3.0'
    testImplementation test.junit
    androidTestImplementation test.ext_junit
    androidTestImplementation test.espresso_core

}

