if (isHomeModule.toBoolean()) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def root_android = rootProject.ext.android
android {
    compileSdkVersion root_android.compileSdkVersion
    buildToolsVersion root_android.buildToolsVersion

    defaultConfig {
        minSdkVersion root_android.minSdkVersion
        targetSdkVersion root_android.targetSdkVersion
        versionCode 1
        versionName "1.0"

        if (!isHomeModule.toBoolean()) {
            //单独调试时添加applicationId集成调试时移除
            applicationId "com.kingz.demo.home"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            // 单独调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (isHomeModule.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            }
        }
    }

    compileOptions {
        // 解决 Invoke-customs are only supported starting with Android O 报错.
        // Reason: kotlin 字节码的问题
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    kotlinOptions {
        // 设置jvm target为1.8 否则某些ktx的语法糖无法使用
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation rootProject.ext.dependencies["appcompat"]
    testImplementation rootProject.ext.test["junit"]
    androidTestImplementation rootProject.ext.test["test-ext-junit"]
    androidTestImplementation rootProject.ext.test["support-test-espresso"]

    implementation project(':module-Common')

    implementation "com.google.code.gson:gson:$lib_versions.gson"

//    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
//    compileOnly rootProject.ext.dependencies["kangaroo"]
//    compileOnly project(":module-Demo")
}
