if (enableHomeModule.toBoolean()) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def root_android = rootProject.ext.android
def root_dependencies = rootProject.ext.dependencies

android {
    compileSdkVersion root_android.compileSdkVersion
    buildToolsVersion root_android.buildToolsVersion

    defaultConfig {
        minSdkVersion root_android.minSdkVersion
        targetSdkVersion root_android.targetSdkVersion
        versionCode 1
        versionName "1.0"

        if (!enableHomeModule.toBoolean()) {
            //单独调试时添加applicationId集成调试时移除
            applicationId "com.kingz.demo.home"
            multiDexEnabled true
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        vectorDrawables.useSupportLibrary = true
        // Arouter的Kotlin配置
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }

    packagingOptions {
        pickFirst 'lib/armeabi/libZekeBitMap.so'
        pickFirst 'lib/x86/libZekeBitMap.so'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            // 单独调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (enableHomeModule.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            }
        }
    }

    compileOptions {
        // 解决 Invoke-customs are only supported starting with Android O 报错.
        // Reason: kotlin 字节码的问题
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    kotlinOptions {
        // 设置jvm target为1.8 否则某些ktx的语法糖无法使用
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation deps.appcompat
    testImplementation test.junit
    androidTestImplementation test.ext_junit
    androidTestImplementation test.espresso_core

    implementation root_dependencies["arouter"]
    kapt root_dependencies["arouter_compiler"]

    implementation project(':module-Common')
    implementation project(':module-Eyepetizer')

    implementation root_dependencies["gson"]
//    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
//    compileOnly rootProject.ext.dependencies["kangaroo"]
//    compileOnly project(":module-Demo")

    implementation "androidx.preference:preference:1.0.0"

    implementation deps.meterial.dialogs
}
